buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.0.3'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group = config.mod_group
archivesBaseName = "${config.mod_name}-${config.mc_version}"

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "run"
    replace "@VERSION@", project.version
    mappings = "${config.forge_mappings}"
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade 'org.apache.httpcomponents:httpclient:4.3.3'
}

reobf{
    jar{
        extraLines "PK: org/apache net/teamio/shade/apache"
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version,
        'modid':config.mod_id, 'name':config.mod_name
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
